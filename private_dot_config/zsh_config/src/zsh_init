# vim:foldmethod=marker

# sk gui height
local SK_HEIGHT=20

# aliases {{{

alias tm="trashman"
alias l="ls -l --color=auto"
alias ls="ls --color=auto"
alias la="ls -a --color=auto"
alias grep="grep --color=auto"
alias fgrep="fgrep --color=auto"
alias egrep="egrep --color=auto"
alias diff="diff --color=auto"
alias cat="bat -ppf"
alias pg="$PAGER"
alias sk="sk -p ' ' --inline-info"
alias wget="wget --hsts-file=${XDG_DATA_HOME}/wget-hsts"
alias v="nvim"
alias suv="sudo nvim"
alias cl="xclip -selection clipboard"
alias ...="cd ../.."
alias ....="cd ../../.."
alias .....="cd ../../../.."
alias ......="cd ../../../../.."

# }}}

# key bindings {{{

# function key bindings
bindkey -M viins "^f" _seek_dir
bindkey -M viins "^v" _seek_file
bindkey -M viins "^[f" _seek_hidden_dir
bindkey -M viins "^[v" _seek_hidden_file
bindkey -M viins "^r" _seek_history
bindkey -M viins "^o" _seek_file_and_open
bindkey -M viins "^p" _seek_proc_by_cpu
bindkey -M viins "^[p" _seek_proc_by_mem
bindkey -M viins "^e" _seek_env_vars
bindkey -M viins "^k" _seek_kill_process
bindkey -M viins "^[m" _quick_mount

# }}}

# git {{{

# Load version control information
autoload -Uz vcs_info
# Format the vcs_info_msg_0_ variable
zstyle ':vcs_info:git:*' formats '(%b) '

alias ,="git"
alias ,s="git status -sb ."
alias ,status="git status"
alias ,l="git log --oneline --graph --decorate -10"
alias ,lo="git log --oneline --graph --decorate"
alias ,log="git log"
alias ,ls="git ls-files -tco"
alias ,b="git branch"
alias ,sb="git show-branch"
alias ,a="git add"
alias ,ch="git checkout"
alias ,d="git diff"
alias ,fetch="git fetch"
alias ,pull="git pull"
alias ,pullr="git pull --rebase"
alias ,pullnr="git pull --no-rebase"
alias ,push="git push"
alias ,commit="git commit"
alias ,merge="git merge"
alias ,apply="git apply"
alias ,rv="git remote -v"
alias ,remote="git remote"
alias ,reset="git reset"
alias ,restore="git restore"
alias ,rm="git rm"
alias ,mv="git mv"
alias ,clone="git clone"

# }}}

# options {{{

# completion
# fpath=($ZSH_BIN $fpath)
autoload -Uz compinit
compinit -d "$XDG_CACHE_HOME/zsh_compdump"
# zstyle ':completion:*' auto-description 'specify: %d'
# zstyle ':completion:*' completer _expand _complete _correct _approximate
# zstyle ':completion:*' format 'Completing %d'
# zstyle ':completion:*' group-name ''
# zstyle ':completion:*' menu select=2
# eval "$(dircolors -b)"
# zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
# zstyle ':completion:*' list-colors ''
# zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
# zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
# zstyle ':completion:*' menu select=long
# zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
# zstyle ':completion:*' use-compctl false
# zstyle ':completion:*' verbose true

# zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
# zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
_comp_options+=(globdots);

# history
HISTFILE="$XDG_CACHE_HOME/zsh_history"
HISTSIZE=1000
SAVEHIST=1000
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt inc_append_history

# type only directory name to cd
setopt auto_cd

# turn off case sensitivity
setopt no_case_glob

# expand prompt substring
setopt PROMPT_SUBST

# }}}

# functions {{{

# search for file with sk
#   if input is empty, open file in nvim
#   if input is not empty, return path of file
function _seek_file()
{
  if [ -z $BUFFER ]; then
    local file=$(fd -E .git -t f | sk --reverse --height=$SK_HEIGHT \
        --preview 'bat -ppf --line-range :500 {}')
    zle reset-prompt
    [ -z $file ] && return
    BUFFER="$EDITOR '$file'"
    zle accept-line
  else
    local file=$(fd -E .git -t f | sk --reverse --height=$SK_HEIGHT)
    zle reset-prompt
    [ -z $file ] && return
    BUFFER+=" '$file'"
    zle vi-end-of-line
  fi
}
zle -N _seek_file

# search for hidden file with sk
#   if input is empty, open hidden file in nvim
#   if input is not empty, return path of hidden file
function _seek_hidden_file()
{
  if [ -z $BUFFER ]; then
    local file=$(fd -E .git -t f -H | rg "^\." | sk --reverse --height=$SK_HEIGHT \
        --preview 'bat -ppf --line-range :500 {}')
    zle reset-prompt
    [ -z $file ] && return
    BUFFER="$EDITOR '$file'"
    zle accept-line
  else
    local file=$(fd -E .git -t f -H | rg "^\." | sk --reverse --height=$SK_HEIGHT)
    zle reset-prompt
    [ -z $file ] && return
    BUFFER+=" '$file'"
    zle vi-end-of-line
  fi
}
zle -N _seek_hidden_file

# search for directory with sk
#   if input is empty, cd into directory
#   if input is not empty, return path of directory
function _seek_dir()
{
  if [ -z $BUFFER ]; then
    local dir=$(fd -t d | sk --reverse --height=$SK_HEIGHT)
    zle reset-prompt
    [ -z $dir ] && return
    BUFFER="cd '$dir'"
    zle accept-line
  else
    local dir=$(fd -t d | sk --reverse --height=$SK_HEIGHT)
    zle reset-prompt
    [ -z $dir ] && return
    BUFFER+=" '$dir'"
    zle vi-end-of-line
  fi
}
zle -N _seek_dir

# search for hidden directory with sk
#   if input is empty, cd into directory
#   if input is not empty, return path of directory
function _seek_hidden_dir()
{
  if [ -z $BUFFER ]; then
    local dir=$(fd -t d -H | rg "^\." | sk --reverse --height=$SK_HEIGHT)
    zle reset-prompt
    [ -z $dir ] && return
    BUFFER="cd '$dir'"
    zle accept-line
  else
    local dir=$(fd -t d -H | rg "^\." | sk --reverse --height=$SK_HEIGHT)
    zle reset-prompt
    [ -z $dir ] && return
    BUFFER+=" '$dir'"
    zle vi-end-of-line
  fi
}
zle -N _seek_hidden_dir

function _seek_file_and_open()
{
  local file=$(fd -E .git -t f | sk --reverse --height=$SK_HEIGHT)
  zle reset-prompt
  [ -z $file ] && return
  xdg-open $file
}
zle -N _seek_file_and_open

# display and search 'ps -aux' sorted by cpu usage
function _seek_proc_by_cpu()
{
  local proc=$(ps axuk %cpu | awk '{print $2"\t"$1"\t"$3" "$4" "$9" "$10" "$11}' \
    | sk --reverse --no-sort --tac \
      --header='id      user    cpu mem start time command')
  zle reset-prompt
  [ -z $proc ] && return
  echo $proc
}
zle -N _seek_proc_by_cpu

# display and search 'ps -aux' sorted by cpu usage
function _seek_proc_by_mem()
{
  local proc=$(ps axuk %mem | awk '{print $2"\t"$1"\t"$4" "$3" "$9" "$10" "$11}' \
    | sk --reverse --no-sort --tac \
      --header='id      user    mem cpu start time command')
  zle reset-prompt
  [ -z $proc ] && return
  echo $proc
}
zle -N _seek_proc_by_mem

function _seek_env_vars()
{
  local env_var=$(env | sk --reverse --tac)
  zle reset-prompt
  [ -z $env_var ] && return
  if [ -z $BUFFER ]; then
    echo $env_var
  else
    BUFFER+=$env_var
    zle vi-end-of-line
  fi
}
zle -N _seek_env_vars

function _seek_kill_process()
{
  local proc=$(ps axuk %cpu | awk '{print $2"\t"$1"\t"$3" "$4" "$9" "$10" "$11}' \
    | sk --reverse --no-sort --tac \
      --header='id      user    cpu mem start time command' | awk '{print $1}')
  zle reset-prompt
  [ -z $proc ] && return
  sudo kill $proc
}
zle -N _seek_kill_process

function _seek_history()
{
  local cmd=$(bat -pp "$HISTFILE" \
    | sk --reverse --tac --no-sort --height=$SK_HEIGHT)
  zle reset-prompt
  [ -z $cmd ] && return
  zle push-input
  BUFFER=$cmd
  zle vi-end-of-line
}
zle -N _seek_history

export mnt="$HOME/.mnt"
function _quick_mount()
{
  [ ! -d $mnt ] && mkdir $mnt
  for m in $(lsblk | tail +2 | awk -F ' ' '{print $7}'); do
    if [[ $m == $mnt ]]; then
      mntdev=$(findmnt $mnt | tail -1 | cut -d' ' -f2)
      echo -n "unmount $mntdev? y/N "
      read -r -k1 yn
      zle reset-prompt
      [[ $yn == 'y' ]] && BUFFER="sudo umount '$mnt'" && zle accept-line
      return
    fi
  done
  lsblk
  local dev=$(fd . '/dev/' | sk --reverse --height=$SK_HEIGHT)
  zle reset-prompt
  [ -z $dev ] && return
  BUFFER="sudo mount '$dev' $mnt"
  zle accept-line
}
zle -N _quick_mount

# run updatedb before locate
function locat()
{
  sudo updatedb
  locate $1
}

# }}}

# plugins {{{

source "$ZSH_BIN/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
source "$ZSH_BIN/zsh-history-substring-search.zsh"
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down
HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND='fg=blue,bold'
HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_NOT_FOUND='fg=red,bold'
eval "$(zoxide init zsh)"

# }}}

# vi mode {{{

# init vi mode
set -o vi

# vi mode
bindkey 'jk' vi-cmd-mode
bindkey 'kj' vi-cmd-mode
bindkey -M viins "^?" backward-delete-char # backspace fix
bindkey -M viins "^W" backward-kill-word
bindkey -M viins "^U" backward-kill-line
bindkey -M viins "^[h" run-help
bindkey -M vicmd "^[h" run-help

# initialize new line in insert mode
function zle-line-init() {
  # zle -K viins
  echo -ne '\e[5 q' # beam
}
zle -N zle-line-init

# set cursor when switching keymaps
function zle-keymap-select () {
  if [[ $KEYMAP == vicmd ]]; then
    echo -ne '\e[1 q' # block
  else
    echo -ne '\e[5 q' # beam
  fi
}
zle -N zle-keymap-select

# }}}
